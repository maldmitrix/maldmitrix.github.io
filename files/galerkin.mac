/************  файл "galerkin.mac" *******************/
/* запускать командой << maxima -b ./galerkin.mac >> */
/******************  НАЧАЛО  *************************/



/*************************** 
**  Подготовительный шаг  **
***************************/

/* Задание линейного дифференциального оператора, 
   функции для правой части уравнения, 
   общего выражения невязки */
L(y,x)  := diff(y,x,2) - 3*diff(y,x,1) + 2*y$
Pfi3(x) := 2*x^2 - 6*x + 2$
R(y,x)  := L(y,x) - Pfi3(x)$

/* Задание главного члена пробной функции 
*  и базисных функций */
u0(x) := 6 - 5*x$
u1(x) := 1 - x + 1/3*x^2$
u2(x) := 1 - x + 1/4*x^3$
u3(x) := 1 - x + 1/5*x^4$
u4(x) := 1 - x + 1/6*x^5$

/* Невязка только от главной функции */
RU0(x) := R(u0(x),x)$

/* Левая часть дифф.уравнения при каждой из базисных функций */
Lu1(x) := L(u1(x),x)$
Lu2(x) := L(u2(x),x)$
Lu3(x) := L(u3(x),x)$
Lu4(x) := L(u4(x),x)$

/* Общее выражение внутреннего 
*  произведения двух произвольных функций */
InProd(y1, y2,x,a,b) := integrate(y1(x)*y2(x),x,a,b)$



/***************** 
**  Шаг первый  **
*****************/

/* Преобразованное условие ортогональности */
K1: InProd(Lu1,u1,x,0,1)$
f1: InProd(RU0,u1,x,0,1)*(-1)$

/* Ищем коэффициент */
c1: f1/K1$

/* Записываем пробное решение */
U1(x) := u0(x) + c1*u1(x)$

/* Невязка на первом шаге */
RU1(x) := R(U1(x),x)$

/*****************
**  Шаг второй  **
*****************/

/* Матричное уравнение ортогональности */
K2: matrix(  [InProd(Lu1,u1,x,0,1),  InProd(Lu2,u1,x,0,1)],
             [InProd(Lu1,u2,x,0,1),  InProd(Lu2,u2,x,0,1)])$

f2: matrix(  [InProd(RU0,u1,x,0,1)*(-1) ],  
                  [InProd(RU0,u2,x,0,1)*(-1) ] )$

/* Ищем коэффициенты */
c2: invert(K2).f2$

/* Записываем пробное решение */
U2(x) := u0(x) + c2[1]*u1(x)+c2[2]*u2(x)$

/* Невязка на втором шаге */
RU2(x) := R(U2(x)[1],x)$



/*****************
**  Шаг третий  **
*****************/

/* Матричное уравнение ортогональности */
K3: matrix(  
    [InProd(Lu1,u1,x,0,1),  InProd(Lu2,u1,x,0,1), 
                 InProd(Lu3,u1,x,0,1) ],
    [InProd(Lu1,u2,x,0,1),  InProd(Lu2,u2,x,0,1), 
                 InProd(Lu3,u2,x,0,1)] , 
    [InProd(Lu1,u3,x,0,1),  InProd(Lu2,u3,x,0,1), 
                 InProd(Lu3,u3,x,0,1)] )$

f3: matrix(  [InProd(RU0,u1,x,0,1)*(-1) ],  
             [InProd(RU0,u2,x,0,1)*(-1) ],
             [InProd(RU0,u3,x,0,1)*(-1) ] )$

/* Ищем коэффициенты */
c3:  invert(K3).f3$

/* Записываем пробное решение */
U3(x) := u0(x) + c3[1]*u1(x) + c3[2]*u2(x) + c3[3]*u3(x)$

/* Невязка на третьем шаге */
RU3(x) := R(U3(x)[1],x)$



/********************
**  Шаг четвертый  **
********************/

/* Матричное уравнение ортогональности */
K4: matrix(  [InProd(Lu1,u1,x,0,1), InProd(Lu2,u1,x,0,1), 
              InProd(Lu3,u1,x,0,1), InProd(Lu4,u1,x,0,1)],
             [InProd(Lu1,u2,x,0,1), InProd(Lu2,u2,x,0,1), 
              InProd(Lu3,u2,x,0,1), InProd(Lu4,u2,x,0,1)], 
             [InProd(Lu1,u3,x,0,1), InProd(Lu2,u3,x,0,1), 
              InProd(Lu3,u3,x,0,1), InProd(Lu4,u3,x,0,1)],
             [InProd(Lu1,u4,x,0,1), InProd(Lu2,u4,x,0,1), 
              InProd(Lu3,u4,x,0,1), InProd(Lu4,u4,x,0,1)])$
                  
f4: matrix(  [InProd(RU0,u1,x,0,1)*(-1)],  
             [InProd(RU0,u2,x,0,1)*(-1)],
             [InProd(RU0,u3,x,0,1)*(-1)],
             [InProd(RU0,u4,x,0,1)*(-1)])$

/* Ищем коэффициенты */
c4:  invert(K4).f4$

/* Записываем пробное решение */
U4(x) := u0(x) + c4[1]*u1(x) + c4[2]*u2(x) 
+ c4[3]*u3(x) + c4[4]*u4(x)$

/* Невязка на третьем шаге */
RU4(x) := R(U4(x)[1],x)$



/*********************
**  Точное решение  **
*********************/
Uexact(x) := 1/2*(%e^2 + 7)/(%e^2 - %e)*%e^x -
(7 + %e)/3/(%e^2 - %e)*%e^(2*x) + x^2$



/**************
**  Графики  **
**************/

/* Графики невязок */
plot2d( [ RU1(x), RU2(x), RU3(x), RU4(x) ], [x,0,1],
 [plot_format, gnuplot],[legend,"Шаг m = 1",
"Шаг m = 2","Шаг m = 3","Шаг m = 4"],
[ylabel, "Невязка R(x)"], [nticks,100])$

/* Графики пробных решений */
plot2d( [ U1(x), U2(x)[1], U3(x)[1], U4(x)[1]], 
[x,0,1], [plot_format, gnuplot],
[legend,"Шаг m = 1", "Шаг m = 2", "Шаг m = 3", "Шаг m = 4"],
[ylabel, "Пробное решение U(x)"], [nticks,100])$

/* График точного решения и последнее пробное решение */
plot2d( [ U4(x)[1], Uexact(x)], [x,0,1], [plot_format, gnuplot],
[legend,"Шаг m = 4", "Точное реш."],
[ylabel, "Пробное решение U(x)"], [nticks,100])$

/******************  КОНЕЦ  ********************/
